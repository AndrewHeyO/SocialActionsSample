apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    implementation project(':social-login-core')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.70'
    implementation('com.google.android.gms:play-services-auth:15.0.1') {
        exclude group: 'com.android.support'
    }
}

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    bintrayName = 'google-explicit'
    artifact = 'google-explicit'
    libraryDescription = 'Social Login - google-explicit'
}

group = rootProject.ext.publishedGroupId
version = rootProject.ext.libraryVersion

install {
    repositories.mavenInstaller {
        pom {
            project {
                groupId rootProject.ext.publishedGroupId
                artifactId artifact

                name rootProject.ext.libraryName
                description libraryDescription
                url rootProject.ext.siteUrl

                licenses {
                    license {
                        name rootProject.ext.licenseName
                        url rootProject.ext.licenseUrl
                    }
                }
                developers {
                    developer {
                        id rootProject.ext.developerId
                        name rootProject.ext.developerName
                        email rootProject.ext.developerEmail
                    }
                }
                scm {
                    connection rootProject.ext.gitUrl
                    developerConnection rootProject.ext.gitUrl
                    url rootProject.ext.siteUrl
                }
            }

            withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = rootProject.ext.bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = rootProject.ext.siteUrl
        issueTrackerUrl = rootProject.ext.issuesUrl
        vcsUrl = rootProject.ext.gitUrl
        licenses = rootProject.ext.allLicenses
        labels = rootProject.ext.tags
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}